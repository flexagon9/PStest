!***********************************************************************
! WP002: Wage progression - Exceptions Report - demo 36
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/17:14:09:16                                 !
!       $Release:  HR92                                                !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
!***********************************************************************
!***********************************************************************

#include 'setenv.sqc'   !Set environment
#include 'setup32.sqc'  !page layout and printer definition

#define col_Sep                    1     !Column separator
#define col_HR_WP_ID               10    !HR WP run id
#define col_Emplid                 11    !Emplid
#define col_EmplRcd                8     !EmplRcd
#define col_Name                   26    !Name
#define col_BARGUnt                15    !Bargaining Unit
#define col_JobCode                8     !Jobcode
#define col_SalPlan                8     !Sal Plan
#define col_Grade                  5     !Grade
#define col_Step                   4     !Step
#define col_ActDate                15    !Activity Date
#define col_Status                 12    !Status
#define col_ExpMess                40    !Exception Message Text


Begin-Heading 10
#Include 'stdhdg01.sqc' !Standard Report header

! Print Title and WP run id on the header
  print $WP002-REPORTTITLE (+1) CENTER
  print 'Wage Progression Run ID:'  (+1,5,25)
  print $RC_HR_WP_ID (0,+{col_sep},{col_HR_WP_ID})

!Column Heading line 1
  print $WP002-EMPLID             (+2,5,{col_Emplid})
  print $WP002-EMPLRCD            (0,+{col_sep},{col_EmplRcd})
  print $WP002-NAME               (0,+{col_sep},{col_Name})
  print $WP002-BARGUNT            (0,+{col_sep},{col_BARGUnt})
  print $WP002-JOBCODE            (0,+{col_sep},{col_Jobcode})
  print $WP002-SALPLAN            (0,+{col_sep},{col_SalPlan})
  print $WP002-SALGRADE           (0,+{col_sep},{col_Grade})
  print $WP002-STEP               (0,+{col_sep},{col_Step})
  print $WP002-ACTDATE            (0,+{col_sep},{col_ActDate})
  print $WP002-STATUS             (0,+{col_sep},{col_Status})
  print $WP002-EXPMESS            (0,+{col_sep},{col_ExpMess})


!Column Heading line 2
  print '='            (+1,5,{col_Emplid})                    FILL
  print '='            (0,+{col_sep},{col_EmplRcd})           FILL
  print '='            (0,+{col_sep},{col_Name})              FILL
  print '='            (0,+{col_sep},{col_BARGUnt})           FILL
  print '='            (0,+{col_sep},{col_Jobcode})           FILL
  print '='            (0,+{col_sep},{col_SalPlan})           FILL
  print '='            (0,+{col_sep},{col_Grade})             FILL
  print '='            (0,+{col_sep},{col_Step})              FILL
  print '='            (0,+{col_sep},{col_ActDate})           FILL
  print '='            (0,+{col_sep},{col_Status})            FILL
  print '='            (0,+{col_sep},{col_ExpMess})           FILL
 
End-Heading

Begin-Report
  do Init-Report
  do Process-Main
  do Reset
  do StdAPI-Term

End-Report

!***********************************************************************
! Init-Report                                                          *
! Called By:    begin-report                                           *
! Calls:        Init-Datetime           - datetime.sqc                 *
!               Get-Current-Datetime    - curdttim.sqc                 *
!               Init-Number             - number.sqc                   *
!               StdAPI-Init             - stdapi.sqc                   *
!               Get-Language-Codes - prcslng.sqc                       *
!               Report-Translation      -                              *
! Description:  Standard PeopleSoft initialization processing.         *
!***********************************************************************

begin-procedure Init-Report
        let $ReportID    = 'WP002'
        do Init-Datetime
        do Get-Current-Datetime
        do Init-Number
        do StdAPI-Init
        if $prcs_process_instance = ''
               display ''
               display 'REPORT CAN NOT BE EXECUTED OUTSIDE OF PEOPLESOFT,PLEASE USE PROCESS SCHEDULER.'
               display ''
               STOP QUIET               
     else
               do Select-Parameters
     end-if
        do Get-Language-Codes
        do Report-Translation
end-procedure ! Init-Report

!***********************************************************************
! Report-Translation                                                   *
! Called By:    Init-Report                                            *
! Calls:        Init_Report_Translation    - sqrtrans.sqc              *
!               Append_Report_Translation  - sqrtrans.sqc              *
!               Get_Field_Information                                  *
! Description:  Performs Strings Table translation.                    *
!***********************************************************************

Begin-Procedure Report-Translation

  do Init_Report_Translation($ReportID, $curr_language_cd)
  do Append_Report_Translation('WP002')

  do Get_Field_Information ('WP002', 'REPORT_TITLE', $WP002-REPORTTITLE, #CW)
  do Get_Field_Information ('WP002', 'HR_WP_ID',     $WP002-HR_WP_ID,    #CW)
  do Get_Field_Information ('WP002', 'EMPLID',       $WP002-EMPLID,      #CW)
  do Get_Field_Information ('WP002', 'EMPL_RCD#',    $WP002-EMPLRCD,     #CW)
  do Get_Field_Information ('WP002', 'NAME',         $WP002-NAME,        #CW)
  do Get_Field_Information ('WP002', 'BARGUNT',      $WP002-BARGUNT,     #CW)
  do Get_Field_Information ('WP002', 'JOBCODE',      $WP002-JOBCODE,     #CW)
  do Get_Field_Information ('WP002', 'SAL_PLAN',     $WP002-SALPLAN,     #CW)
  do Get_Field_Information ('WP002', 'GRADE',        $WP002-SALGRADE,    #CW)
  do Get_Field_Information ('WP002', 'STEP',         $WP002-STEP,        #CW)
  do Get_Field_Information ('WP002', 'ACTDATE',      $WP002-ACTDATE,     #CW)
  do Get_Field_Information ('WP002', 'STATUS',       $WP002-STATUS,      #CW)
  do Get_Field_Information ('WP002', 'EXPMESS',      $WP002-EXPMESS,     #CW)

End-Procedure

!***********************************************************************
! Report-Translation                                                   *
! Called By:    Begin-Report section                                   *
! Calls:                                                               *
!                                                                      *
! Description:  Main select logic for report.                          *
!***********************************************************************

Begin-Procedure Process-Main

Begin-Select
A.EMPLID                (+1,5,{col_Emplid})
A.EMPL_RCD              (0,+{col_sep},{col_EmplRcd})
B.NAME                  (0,+{col_sep},{col_Name})
A.BARG_UNIT             (0,+{col_sep},{col_BARGUnt})
A.JOBCODE               (0,+{col_sep},{col_JobCode})
A.SAL_ADMIN_PLAN        (0,+{col_sep},{col_SalPlan})
A.GRADE                 (0,+{col_sep},{col_Grade})
A.STEP                  (0,+{col_sep},{col_Step})

A.ACTIVITY_DT
        do Format-DateTime(&A.ACTIVITY_DT, $ACTOut, {DEFDATE}, '', '')
        print $ACTOut   (0,+{col_sep},{col_ACTDate})

A.HR_WP_PROG_STATUS

        Let $Fieldname = 'HR_WP_PROG_STATUS'
        Let $FieldValue = &A.HR_WP_PROG_STATUS
         do Read-Translate-Table
        print $XlatShortName (0,+{col_sep},{col_Status})

C.MESSAGE_TEXT          (0,+{col_sep},{col_ExpMess})
      
FROM  PS_HR_WP_ACTIVITY A,
      PS_PERSON_NAME B,
      PSMSGCATDEFN C

WHERE A.EMPLID = B.EMPLID
    AND A.HR_WP_ID = $RC_HR_WP_ID
    AND A.HR_WP_EXCPTN_FLAG = 'Y'
    AND A.HR_WP_EXCPTN_APPR = 'N'
    AND A.MESSAGE_SET_NBR = C.MESSAGE_SET_NBR
    AND A.MESSAGE_NBR = C.MESSAGE_NBR

ORDER BY B.NAME , A.ACTIVITY_DT

End-Select
End-Procedure

!*************************************

#Include 'stdapi.sqc'     !Update Process API
#Include 'wprnctl1.sqc'   !Get application run control parameters
#Include 'curdttim.sqc'   !Get-Current-DateTime procedure
#include 'prcslng.sqc'    !Replaces getrplng.sqc
#include 'rellang.sqc'    !Related Language File
#include 'number.sqc'     !Routines to format numbers
#include 'datetime.sqc'   !Routines for date and time formatting
#Include 'reset1.sqc'     !Reset printer procedure
#Include 'readxlat.sqc'   !get translates for fieldname, value
#Include 'sqrtrans.sqc'   !Translate SQR strings to given language

!******************************* End Of Report *******************************
